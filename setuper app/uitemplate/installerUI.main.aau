import win.ui;
import win.animate;
import key.hook;
import console;
import MessageMultiLanguage;
import ResourceRelease;

//import com.interface.ITaskbarList3;
/*DSG{{*/
winform = ..win.form(text="Setuper";right=650;bottom=523;bgcolor=4734261;border="none")
winform.add(
btnbrowser={cls="plus";text="浏览...";left=406;top=482;right=496;bottom=505;background="\res\btn_browser_bkg.png";notify=1;z=6};
btnSetup={cls="plus";text="安装";left=502;top=482;right=599;bottom=504;background="\res\btn_install_bkg.png";z=7};
ButtonClose={cls="plus";left=612;top=0;right=648;bottom=30;notify=1;repeat="center";z=1};
ButtonMin={cls="plus";left=579;top=0;right=610;bottom=30;notify=1;repeat="center";z=2};
edit={cls="edit";left=47;top=482;right=400;bottom=504;bgcolor=16777215;db=1;dr=1;edge=1;font=LOGFONT( name='宋体' );hidesel=1;tabstop=1;z=5};
PictureViwer={cls="plus";left=0;top=33;right=648;bottom=433;foreRepeat="strech";repeat="strech";z=4};
progress={cls="plus";left=0;top=435;right=652;bottom=467;align="right";background="\res\progress-bg.jpg";bgcolor=65280;color=2719744;font=LOGFONT( h=-16 );foreground="\res\progress.jpg";foreRepeat="expand";z=3};
quickLoad={cls="plus";text="立即体验";left=272;top=479;right=377;bottom=508;background="\res\btn_browser_bkg.png";notify=1;z=9};
title={cls="static";text="static";left=16;top=6;right=100;bottom=31;color=16777215;font=LOGFONT( h=-13 );transparent=1;z=8}
)
/*}}*/

winform.quickLoad.skin(
	background = { 
		hover = "/res/btn_hover.png";
		active = "/res/btn_active.png";
		default = "/res/btn_default.png"; 
		disabled = "/res/btn_disabled.png"; 
	}
)

//最小化按钮	
winform.ButtonMin.skin(
     background = { 
         hover = "/res/min-hover.png";
         active = "/res/min-active.png";
         default = "/res/min-default.png"; 
     }
    color = {
        hover = 0xFFFF0000; //鼠标移上去的颜色
        active = 0xFF00FF00; //鼠标按下去的颜色
    } 
)
winform.ButtonMin.oncommand = function(id,event){
	winform.hitmin();
}

//关闭按钮
winform.ButtonClose.skin(
	background = { 
		hover = "/res/close-hover.png";
		active = "/res/close-active.png";
		default = "/res/close-default.png"; 
	}
	color = {
		hover = 0xFFFF0000; //鼠标移上去的颜色
        active = 0xFF00FF00; //鼠标按下去的颜色
	}
)
winform.ButtonClose.show(true);
winform.ButtonClose.oncommand = function( id,event ){
	winform.close();
}

// 安装按钮
winform.btnSetup.skin(
     background = { 
        hover = "/res/btn_hover.png";
		active = "/res/btn_active.png";
		default = "/res/btn_default.png"; 
		disabled = "/res/btn_disabled.png"; 
     }
)

// 浏览按钮
winform.btnbrowser.skin(
     background = { 
        hover = "/res/btn_hover.png";
		active = "/res/btn_active.png";
		default = "/res/btn_default.png"; 
		disabled = "/res/btn_disabled.png"; 
     }
)

/////////////////////////////////////////////////////////////////
//	UI事件处理
/////////////////////////////////////////////////////////////////

// 立即体验按钮响应函数
winform.quickLoad.oncommand = function(id,event){
	import process;
	import io;

	var targetPath = winform.edit.text;
	var targetExe = fsys.joinpath(targetPath, ResourceRelease.g_PackageJsonManager["outputfoldername"] + ".exe"); //设置目标路径
	if(io.exist(targetExe)){
		process.execute(targetExe);
	}

	winform.close();
}

//	安装按钮响应函数
winform.btnSetup.oncommand = function(id,event){
	
	winform.edit.disabled=true;
	winform.btnbrowser.disabled=true;
	winform.btnSetup.disabled=true;
	
	//	启动解压安装线程
	import ResourceInstall;
	hThread = thread.create( ResourceInstall.Task_Install,
						winform.edit.text,ResourceRelease.g_OutputFolderName/*,winform.pie.hwnd*/,
						fv_thread_id, 
						ResourceRelease.g_PackageJsonData );
	thread.waitOne(hThread);
	
	winform.edit.disabled=false;
	winform.btnbrowser.disabled=false;
	winform.btnSetup.disabled=false;
}
 
 // 浏览按钮响应函数
winform.btnbrowser.oncommand = function(id,event){
	import fsys.dlg;
    var targetDir = fsys.dlg.opendir(,,"请选择安装目录");
    if(!targetDir)return; 
    winform.edit.text=targetDir;
}

// 监听键盘按钮事件
// 事件处理得不对，会使edit不能键盘输入
/*****
var hook = key.hook();
//录制回调函数
hook.proc = function(msg,vkcode,scancode,injected,flags,timeStamp,extraInfo){ 
	if( injected ) return; //模拟鼠标不处理
  
    var kn = key.getName( vkcode ); 
    	
    select(msg) {
    	case 0x100/*_WM_KEYDOWN*/ ,0x104/*_WM_SYSKEYDOWN*/ {
    	    //console.log("按下","键名:" + kn,"键码:"+vkcode)
    	    if(kn == "ESC"){
    	    	winform.close();
    	    };
    	}
    	case 0x101/*_WM_KEYUP*/,0x105/*_WM_SYSKEYUP*/ {
    	    //console.log("弹起","键名:" + kn,"键码:"+vkcode)
    	}  
    } 
    
    if( win.getForeground() == winform.hwnd ) return true;  
}*****/


//添加下面的代码以支持鼠标拖动窗体
winform.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) {
		/*任务栏显示进度*/ 
		/*case _WM_TASKBARBUTTONCREATED{ 
			winform.taskbar = com.interface.ITaskbarList3.Create();
		} */
		case 0x201/*窗体拖动 */{
			winform.hitCaption();
		}
	} 
}

// 程序显示关闭动画
winform.onClose = function(hwnd,message,wParam,lParam){
    win.animate.fade(winform).hide(150)
}

//////////////////////////////////////////////////////////////////
//	进度条显示刷新
//////////////////////////////////////////////////////////////////

//	进度条更新，[问题]频率高卡主窗口，控制频率
import thread.command;
var listener = thread.command();
listener.print = function( percent ){
	if(percent <= 100 ){
		winform.progress.text = percent + "%";
		winform.progress.progressPercentage = percent;
	}
	//winform.taskbar.SetProgressValue(winform.hwnd, percent, 100);
}
 
listener.extract_finish = function(finish){
	if (finish == 100){
		winform.edit.show(false);
		winform.btnbrowser.show(false);
		winform.btnSetup.show(false);
		winform.quickLoad.show(true);
		
		import fsys.lnk;
		var targetPath = winform.edit.text;
		var exePath = fsys.joinpath(targetPath, ResourceRelease.g_PackageJsonManager["softwarename"] + ".exe")
		if (ResourceRelease.g_PackageJsonManager["desktoplink_on"]){
			if(ResourceRelease.g_PackageJsonManager["desktoplink_on"] == "true" && io.exist(exePath)){
				desktop_lnk = fsys.lnk(); 
				desktop_lnk.name = ResourceRelease.g_PackageJsonManager["desktoplinkname"]; 
				//http://bbs.aau.cn/forum.php?mod=viewthread&tid=3963&page=1#pid22739
				desktop_lnk.path = exePath; //设置目标路径
				desktop_lnk.setIcon(desktop_lnk.path, 0); //设置图标
				desktop_lnk.workDir = targetPath;         //起始位置
				desktop_lnk.showCmd = 1;//运行方式：常规窗口=1,最小化=2，最大化=3
				desktop_lnk.save(fsys.joinpath( fsys.getSpecial(0x0010 /*_CSIDL_DESKTOPDIRECTORY*/ ), ResourceRelease.g_PackageJsonManager["outputfoldername"] + ".lnk"));
			}
		}
	}
}



//////////////////////////////////////////////////////////////////
//	初始化
//////////////////////////////////////////////////////////////////

// 单例运行
import process.mutex
mutex = process.mutex("installer_ui_process_id")
if( mutex.conflict ){
	winform.msgbox("安装程序已经运行!"); // 4
	return;
}

//	释放资源文件
var resource_release_result, n_error_msg_code = ResourceRelease.ReleaseResourceFromEXE();
if ( !resource_release_result ){
	winform.msgbox( MessageMultiLanguage.GetErrorMessage( ResourceRelease.g_PackageJsonManager['language'], n_error_msg_code ) );
	return ; 
}

// 设置图片播放器
var json_ppt_order=ResourceRelease.g_PackageJsonManager['ppt_order'];
if(null != json_ppt_order)
{
	var min, max = table.range(json_ppt_order);
	if (max != 0)
	{
		winform.PictureViwer.setBackground(ResourceRelease.g_OutputFolderName+json_ppt_order[1]);
	
		//定时播放图片
		var i = 2;
		var tmId = winform.addtimer( 
			1500,
			function(hwnd,msg,id,tick){

				var json_ppt_order=ResourceRelease.g_PackageJsonManager['ppt_order'];
				if (json_ppt_order[i]){
					var picturePath = ResourceRelease.g_OutputFolderName + json_ppt_order[i];
					winform.PictureViwer.setForeground(picturePath);
				}
				
				if(i < #json_ppt_order){
					i = i + 1
				}
				else {
					i = 1;
				}
			
			} 
		)
	}
	else {
		winform.PictureViwer.setBackground("\res\picture_viwer_default.png");
	}
	
	}
else {
	winform.PictureViwer.setBackground("\res\picture_viwer_default.png");
}


////////////////////////////////////////////////////////////////////////
//	UI初始化
////////////////////////////////////////////////////////////////////////

//	界面国际化
if ( "en" == ResourceRelease.g_PackageJsonManager['language'] ){
	winform.btnbrowser.text='Browser...';
	winform.btnSetup.text='Setup';
	winform.quickLoad.text='Run at once';
}
else if ( "zh-TW" == ResourceRelease.g_PackageJsonManager['language'] ){
	winform.btnbrowser.text='g[...';
	winform.btnSetup.text='安b';
	winform.quickLoad.text='立即w';
}
else if ( "zh-CN" == ResourceRelease.g_PackageJsonManager['language'] ){//默认中文
}

//	修改主窗口背景色
if ( null != ResourceRelease.g_PackageJsonManager['background-color'] ){
	winform.bgcolor = tonumber( ResourceRelease.g_PackageJsonManager['background-color'], 16);
}

//隐藏快速体验按钮
winform.quickLoad.show(false);

// 进度条范围设定，接受解压线程的值
winform.progress.setProgressRange(1,100)

import fsys;
winform.edit.text=fsys.getSpecial(0x26)+"\"+ ResourceRelease.g_PackageJsonManager['outputfoldername'];
winform.title.text = ResourceRelease.g_PackageJsonManager['outputfoldername'];

winform.show(true);
win.loopMessage();  
//hook.close();
